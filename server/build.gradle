plugins {
    id 'application'

    // 不加入此会出现找不到模块错误
    id 'org.javamodularity.moduleplugin' version '2.0.0'

    // 请使用高版本，'2.26.0'实测出现 "Unsupported class file major version 65"错误
    id 'org.beryx.jlink' version '3.1.3'

    // Shadow 插件: 用于将所有依赖打包进同一个 JAR 文件中
    id 'com.gradleup.shadow' version '9.2.2'

    id 'idea'
    id 'java'
}


// java版本选择25（LTS）
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(25))
    }
}

version = '2.3.0'


application{
    mainModule.set("quickoutline")
    mainClass.set("com.ririv.quickoutline.server.SidecarApp")
}


//// 不设置这个idea,没法下载文档
//// 注意更改系统
//configurations.configureEach {
//    attributes {
//        attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
//        attribute(OperatingSystemFamily.OPERATING_SYSTEM_ATTRIBUTE, objects.named(OperatingSystemFamily, 'windows')) // 根据您的操作系统调整
//        attribute(MachineArchitecture.ARCHITECTURE_ATTRIBUTE, objects.named(MachineArchitecture, 'x86-64')) // 根据您的架构调整
//    }
//}



def itextVersion = '9.2.0'

// 不能下载请配置gradle代理
dependencies {

    // 使用该依赖打包会出现 错误: 当前不支持已签名模块化 JAR ..\QuickOutline\build\jlinkbase\jlinkjars\bcpkix-jdk18on-1.78.jar,
//    https://stackoverflow.com/questions/50597926/signed-modular-jar-with-crypto-provider-cannot-be-linked-into-run-time-image
    implementation "com.itextpdf:bouncy-castle-adapter:${itextVersion}" //签名用，加密的PDF必须添加
    implementation "com.itextpdf:kernel:${itextVersion}"
    implementation "com.itextpdf:io:${itextVersion}"
    implementation "com.itextpdf:layout:${itextVersion}"
    implementation "com.itextpdf:font-asian:${itextVersion}"

    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'

    implementation 'org.slf4j:slf4j-api:2.0.16' // Use the latest stable version
    implementation 'org.slf4j:slf4j-simple:2.0.16' // Or another implementation

    // Apache PDFBox 核心库
    implementation 'org.apache.pdfbox:pdfbox:3.0.3'
//    implementation 'org.apache.pdfbox:pdfbox-tools:3.0.3'
    // 如果需要字体支持（比如渲染中文等非西文字符）
    implementation 'org.apache.pdfbox:fontbox:3.0.3'

    implementation 'com.google.code.gson:gson:2.13.2'

    implementation 'jakarta.inject:jakarta.inject-api:2.0.1'
//    implementation("com.openai:openai-java:3.1.2")

    implementation "com.itextpdf:html2pdf:6.2.1"

    // Apache Batik for SVG rendering
    implementation 'de.rototor.pdfbox:graphics2d:3.0.1'
    implementation('org.apache.xmlgraphics:batik-transcoder:1.19') {
        exclude group: 'xml-apis', module: 'xml-apis'
    }

    implementation 'org.jfree:org.jfree.svg:5.0.7'


    implementation 'io.vertx:vertx-core:5.0.5'
    // [macOS] 消除 Netty DNS Resolver 警告 (可选)
    // 这是一个 macOS 特有的优化库。如果你的开发环境是 macOS，添加此依赖可消除警告。
    // classifiers: osx-aarch_64 (Apple Silicon M1/M2...), osx-x86_64 (Intel Mac)
    // 注意：版本号建议检查 Vert.x 5.0.0 依赖树中的 netty-common 版本，这里暂用较新版本
    runtimeOnly 'io.netty:netty-resolver-dns-native-macos:4.2.7.Final:osx-aarch_64'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:6.0.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:6.0.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:6.0.1'
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.14.0"

}

repositories {
    //使用https，http会报警告
//    maven { url 'https://maven.aliyun.com/repository/public/' }
//    maven { url "https://maven.aliyun.com/repository/gradle-plugin" }
//    maven { url 'https://mirrors.cloud.tencent.com/gradle/'}
    gradlePluginPortal()
    mavenLocal()
    mavenCentral()
}

//传递版本号给Java程序，将在System.System.getProperty("app.version")获取
def args = [
    '-Dapp.version=' + project.version,
    // 屏蔽iText的字形缺失警告
    '-Dorg.slf4j.simpleLogger.log.com.itextpdf.kernel.font.PdfType1Font=error',
    // 屏蔽PDFBox的字形缺失警告
    '-Dorg.slf4j.simpleLogger.log.org.apache.pdfbox.rendering.GlyphCache=error',
    // 屏蔽PDFBox fontbox的字符串命令警告
    '-Dorg.slf4j.simpleLogger.log.org.apache.fontbox.cff.Type1CharString=error',
    // 【新增】屏蔽 @keyframes 不支持的错误日志
    // 将其级别设置为 OFF (完全关闭) 或者 FATAL (只有崩溃才报)
    // 注意：类名必须完全匹配报错信息中的类名
    '-Dorg.slf4j.simpleLogger.log.com.itextpdf.styledxmlparser.css.parse.syntax.CssParserStateController=OFF',
    // https://github.com/openjdk/jfx/blob/jfx25/doc-files/release-notes-24.md#javafx-applications-must-use---enable-native-access
    '--enable-native-access=ALL-UNNAMED',
    '-Djavafx.enablePreview=true',
//    '-Dprism.order=es2,d3d',
//    '-Dprism.verbose=true', // 开发时开启，发布时关掉
//    '-Dprism.lcdtext=false' // 视效果而定
    '-Djavafx.animation.fullspeed=true',
    '-Djavafx.pulseLogger=false',
    '-Dprism.lcdtext=false',

//    AWT 触发：你的某个功能（可能是生成 PDF、处理图片、或者仅仅是计算字体宽度）在底层调用了 java.awt 包相关的类（比如 java.awt.Font, java.awt.Color, javax.imageio.ImageIO 等）。
//    默认行为：在 macOS/Windows 上，一旦 Java 程序加载了 AWT 图形库（即使你不显示任何窗口），操作系统就会默认把它当做一个“图形界面应用程序”，因此会在 Dock 栏显示一个 Java 图标（通常是那个咖啡杯）。
    // 【新增修复】开启 Headless 模式，禁止显示 Dock 图标
    '-Djava.awt.headless=true'
]

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
//    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" // 调试用
}



// 仅对application中的run任务有效，对build中任务无效
run {
    jvmArgs = args
}


def os = org.gradle.internal.os.OperatingSystem.current()

// https://badass-jlink-plugin.beryx.org/releases/latest/
// 仅对build中任务有效，对application中的run任务无效，
jlink {
    // 注意参数是jar名的前缀（String... jarPrefixes）
    addExtraDependencies("jackson-core")

    launcher {
        name = 'QuickOutlineSidecar'
        jvmArgs = args
    }

   // https://github.com/openjdk/jfx/blob/jfx25/doc-files/release-notes-24.md#the-jdkjsobject-module-is-now-included-with-javafx
   // 确保使用 JavaFX 自带的 jdk.jsobject 模块：
   // 将 JavaFX jmods 放在 JDK jmods 之前，使其作为可升级模块覆盖 JDK 中的同名模块。
   // 注意：请将下面的路径替换为你本机 JavaFX jmods 实际位置（例如：/Users/wuhao/Downloads/javafx-jmods-25）。
   // 如果你使用的是本地解压的 JavaFX SDK：jmods 目录一般在 sdk 根目录的 "jmods" 子目录中。
   // 示例：/path/to/javafx-jmods-25 或 /path/to/javafx-sdk-25/jmods
//    def javafxJmodsPath = System.getenv('JAVAFX_JMODS')
//    if (javafxJmodsPath != null && !javafxJmodsPath.isBlank()) {
//        addExtraModulePaths(files(javafxJmodsPath))
//    }
//    压缩问题：https://github.com/fvarrui/JavaPackager/issues/383
//     “警告: 当前不支持已签名模块化" 加入'--ignore-signing-information'选项
    options = ['--strip-debug', '--compress', 'zip-6', '--no-header-files', '--no-man-pages', '--ignore-signing-information']
//    https://walczak.it/blog/distributing-javafx-desktop-applications-without-requiring-jvm-using-jlink-and-jpackage

//    配置参考文档 https://docs.oracle.com/en/java/javase/21/docs/specs/man/jpackage.html
    jpackage {
//        skipInstaller = true

        // --- 核心修正 ---
        // 1. 使用插件的专用属性来设置版本和开发者信息，避免命令行参数冲突
        appVersion = project.version.toString()
        vendor = 'riri Personal'

        // 2. 移除错误的 installerOptionsParams 定义，因为它会导致参数重复
        // def installerOptionsParams = [ ... ] // <--- 整个错误定义的代码块已被删除

        println('Current OS: '+os)


        // 安装器类型和图标（按平台动态配置）
        if (os.isWindows()) {
            installerType = 'msi'
            icon = 'src/main/resources/icon/icon.ico'
            installerOptions = [
                    '--win-shortcut',
                    '--win-dir-chooser',
                    '--win-menu'
            ]
        } else if (os.isMacOsX()) {
            installerType = 'dmg'
            icon = 'src/main/resources/icon/icon.icns'
            installerOptions = [
                    '--mac-package-name', 'QuickOutline',
                    '--mac-package-identifier', 'com.ririv.quickoutline'
            ]
        } else if (os.isLinux()) {
            installerType = 'deb' // 或 'rpm'（根据需求选择）
            icon = 'src/main/resources/icon/icon.png' // Linux 支持 PNG/SVG
            installerOptions = [
                    '--linux-shortcut', // 创建桌面快捷方式
                    '--linux-menu-group', 'Development', // 应用被如何归类，有一些标准并且常用的类别名称
                    '--linux-package-name', 'quickoutline' // 包名
            ]
        }
    }
}

test {
    useJUnitPlatform()
    enabled = false // 禁用测试任务
}

shadowJar {
    // 设置最终 Fat JAR 的文件名
    archiveClassifier.set('all') // 生成如 'your-app-version-all.jar'

    // 排除签名文件和不必要的元数据，减小体积并避免冲突
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/LICENSE*'
    exclude 'META-INF/NOTICE*'
    exclude 'META-INF/README*'
    exclude '**/module-info.class' // 如果项目是非模块化或自动模块，排除模块描述符，避免潜在的冲突

    // 处理 SPI (Service Provider Interface) 配置文件冲突，例如 Vert.x, SLF4J
    // 当多个 JAR 包含 META-INF/services/ 下的同名文件时，需要合并内容
    mergeServiceFiles()

    // 解决其他常见的文件冲突，例如属性文件或原生库
    // 通常 Vert.x/Netty 会导致一些 META-INF/io.netty 路径下的重复，或者 Log4j
    // append 'META-INF/io.netty.versions.properties' // 示例：如果出现此文件冲突
    // append 'META-INF/native-image/io.netty/common/reflect-config.json' // 示例

    // 确保主类被正确写入 JAR 的 Manifest 文件
    manifest {
        attributes 'Main-Class': application.mainClass.get() // 从 application 插件获取主类
    }

    // 暂时不开启 minimize()，因为 Vert.x/Jackson 等库大量使用反射，
    // minimize() 容易误删运行时需要的类，导致 NoClassDefFoundError。
    // 如果需要进一步裁剪，可以在功能验证稳定后，谨慎尝试并手动维护保留规则。
    // minimize() {
    //     // 例如：保留所有 Vert.x 相关的类
    //     keep 'io.vertx.**'
    //     // 排除测试相关的依赖
    //     exclude(dependency('org.junit.*'))
    // }
}
