plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'

    // 参考 https://github.com/openjfx/javafx-gradle-plugin#extra-plugins
    // 不加入此会出现找不到模块错误
    id 'org.javamodularity.moduleplugin' version '2.0.0'

    // 请使用高版本，'2.26.0'实测出现 "Unsupported class file major version 65"错误
    id 'org.beryx.jlink' version '3.1.3'

    id 'idea'
    id 'java'
}


// java版本选择25（LTS）
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(25))
    }
}

version = '2.3.0'

javafx {
    version = '25'
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.swing', 'javafx.web' ]
}

application{
    mainModule.set("quickoutline")
    mainClass.set("com.ririv.quickoutline.view.App")
}


//// 不设置这个idea,没法下载文档
//// 注意更改系统
//configurations.configureEach {
//    attributes {
//        attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
//        attribute(OperatingSystemFamily.OPERATING_SYSTEM_ATTRIBUTE, objects.named(OperatingSystemFamily, 'windows')) // 根据您的操作系统调整
//        attribute(MachineArchitecture.ARCHITECTURE_ATTRIBUTE, objects.named(MachineArchitecture, 'x86-64')) // 根据您的架构调整
//    }
//}



def itextVersion = '9.2.0'

// 不能下载请配置gradle代理
dependencies {

    // 使用该依赖打包会出现 错误: 当前不支持已签名模块化 JAR ..\QuickOutline\build\jlinkbase\jlinkjars\bcpkix-jdk18on-1.78.jar,
//    https://stackoverflow.com/questions/50597926/signed-modular-jar-with-crypto-provider-cannot-be-linked-into-run-time-image
    implementation "com.itextpdf:bouncy-castle-adapter:${itextVersion}" //签名用，加密的PDF必须添加
    implementation "com.itextpdf:kernel:${itextVersion}"
    implementation "com.itextpdf:io:${itextVersion}"
    implementation "com.itextpdf:layout:${itextVersion}"
    implementation "com.itextpdf:font-asian:${itextVersion}"

    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'

    implementation 'org.slf4j:slf4j-api:2.0.16' // Use the latest stable version
    implementation 'org.slf4j:slf4j-simple:2.0.16' // Or another implementation

    // Apache PDFBox 核心库
    implementation 'org.apache.pdfbox:pdfbox:3.0.3'
//    implementation 'org.apache.pdfbox:pdfbox-tools:3.0.3'
    // 如果需要字体支持（比如渲染中文等非西文字符）
    implementation 'org.apache.pdfbox:fontbox:3.0.3'

    // Guice for Dependency Injection
    implementation 'com.google.inject:guice:7.0.0'
    // Force Guava to a newer version to fix CVE-2023-2976                                                  │
    implementation 'com.google.guava:guava:33.5.0-jre'
    implementation 'jakarta.inject:jakarta.inject-api:2.0.1'
//    implementation("com.openai:openai-java:3.1.2")

    implementation "com.itextpdf:html2pdf:6.2.1"

    // Apache Batik for SVG rendering
    implementation('org.apache.xmlgraphics:batik-transcoder:1.19') {
        exclude group: 'xml-apis', module: 'xml-apis'
    }

    // Commonmark for Markdown processing
    implementation 'org.commonmark:commonmark:0.27.0'
    implementation 'org.commonmark:commonmark-ext-gfm-tables:0.27.0' // For table support

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.14.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.14.0'
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.14.0"
    
}

repositories {
    //使用https，http会报警告
//    maven { url 'https://maven.aliyun.com/repository/public/' }
//    maven { url "https://maven.aliyun.com/repository/gradle-plugin" }
//    maven { url 'https://mirrors.cloud.tencent.com/gradle/'}
    gradlePluginPortal()
    mavenLocal()
    mavenCentral()
}

//传递版本号给Java程序，将在System.System.getProperty("app.version")获取
def args = [
    '-Dapp.version=' + project.version,
    // 屏蔽iText的字形缺失警告
    '-Dorg.slf4j.simpleLogger.log.com.itextpdf.kernel.font.PdfType1Font=error',
    // 屏蔽PDFBox的字形缺失警告
    '-Dorg.slf4j.simpleLogger.log.org.apache.pdfbox.rendering.GlyphCache=error',
    // 屏蔽PDFBox fontbox的字符串命令警告
    '-Dorg.slf4j.simpleLogger.log.org.apache.fontbox.cff.Type1CharString=error',
    // https://github.com/openjdk/jfx/blob/jfx25/doc-files/release-notes-24.md#javafx-applications-must-use---enable-native-access
    '--enable-native-access=javafx.graphics,javafx.web',
    '-Djavafx.enablePreview=true'
]

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
//    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" // 调试用
}



// 仅对application中的run任务有效，对build中任务无效
run {
    jvmArgs = args
}


def os = org.gradle.internal.os.OperatingSystem.current()

// https://badass-jlink-plugin.beryx.org/releases/latest/
// 仅对build中任务有效，对application中的run任务无效，
jlink {
    launcher {
        name = 'QuickOutline'
        jvmArgs = args
    }

   // https://github.com/openjdk/jfx/blob/jfx25/doc-files/release-notes-24.md#the-jdkjsobject-module-is-now-included-with-javafx
   // 确保使用 JavaFX 自带的 jdk.jsobject 模块：
   // 将 JavaFX jmods 放在 JDK jmods 之前，使其作为可升级模块覆盖 JDK 中的同名模块。
   // 注意：请将下面的路径替换为你本机 JavaFX jmods 实际位置（例如：/Users/wuhao/Downloads/javafx-jmods-25）。
   // 如果你使用的是本地解压的 JavaFX SDK：jmods 目录一般在 sdk 根目录的 "jmods" 子目录中。
   // 示例：/path/to/javafx-jmods-25 或 /path/to/javafx-sdk-25/jmods
//    def javafxJmodsPath = System.getenv('JAVAFX_JMODS')
//    if (javafxJmodsPath != null && !javafxJmodsPath.isBlank()) {
//        addExtraModulePaths(files(javafxJmodsPath))
//    }
//    压缩问题：https://github.com/fvarrui/JavaPackager/issues/383
//     “警告: 当前不支持已签名模块化" 加入'--ignore-signing-information'选项
    options = ['--strip-debug', '--compress', 'zip-6', '--no-header-files', '--no-man-pages', '--ignore-signing-information']
//    https://walczak.it/blog/distributing-javafx-desktop-applications-without-requiring-jvm-using-jlink-and-jpackage

//    配置参考文档 https://docs.oracle.com/en/java/javase/21/docs/specs/man/jpackage.html
    jpackage {
//        skipInstaller = true

        // --- 核心修正 ---
        // 1. 使用插件的专用属性来设置版本和开发者信息，避免命令行参数冲突
        appVersion = project.version.toString()
        vendor = 'riri Personal'

        // 2. 移除错误的 installerOptionsParams 定义，因为它会导致参数重复
        // def installerOptionsParams = [ ... ] // <--- 整个错误定义的代码块已被删除

        println('Current OS: '+os)


        // 安装器类型和图标（按平台动态配置）
        if (os.isWindows()) {
            installerType = 'msi'
            icon = 'src/main/resources/com/ririv/quickoutline/view/images/icon.ico'
            installerOptions = [
                    '--win-shortcut',
                    '--win-dir-chooser',
                    '--win-menu'
            ]
        } else if (os.isMacOsX()) {
            installerType = 'dmg'
            icon = 'src/main/resources/com/ririv/quickoutline/view/images/icon.icns'
            installerOptions = [
                    '--mac-package-name', 'QuickOutline',
                    '--mac-package-identifier', 'com.ririv.quickoutline'
            ]
        } else if (os.isLinux()) {
            installerType = 'deb' // 或 'rpm'（根据需求选择）
            icon = 'src/main/resources/com/ririv/quickoutline/view/images/icon.png' // Linux 支持 PNG/SVG
            installerOptions = [
                    '--linux-shortcut', // 创建桌面快捷方式
                    '--linux-menu-group', 'Development', // 应用被如何归类，有一些标准并且常用的类别名称
                    '--linux-package-name', 'quickoutline' // 包名
            ]
        }
    }
}

test {
    useJUnitPlatform()

    enabled = false // 禁用测试任务

}




