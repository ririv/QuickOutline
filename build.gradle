plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'

    // 参考 https://github.com/openjfx/javafx-gradle-plugin#extra-plugins
    // 不加入此会出现找不到模块错误
    id 'org.javamodularity.moduleplugin' version '1.8.15'

    // 请使用高版本，'2.26.0'实测出现 "Unsupported class file major version 65"错误
    id 'org.beryx.jlink' version '3.1.1'

    id 'idea'
    id 'java'
}

//java {
//    modularity.inferModulePath.set(false)
//}

// java版本选择21（LTS）
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

version = '2.0.0'

javafx {
    version = '23' // LTS
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web']
}

application{
    mainModule.set("quickoutline")
    mainClass.set("com.ririv.quickoutline.view.App")
}


//// 不设置这个idea,没法下载文档
//// 注意更改系统
//configurations.configureEach {
//    attributes {
//        attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
//        attribute(OperatingSystemFamily.OPERATING_SYSTEM_ATTRIBUTE, objects.named(OperatingSystemFamily, 'windows')) // 根据您的操作系统调整
//        attribute(MachineArchitecture.ARCHITECTURE_ATTRIBUTE, objects.named(MachineArchitecture, 'x86-64')) // 根据您的架构调整
//    }
//}



def itextVersion = '9.0.0'

// compile 不要用了，已经废弃了
// 不能下载请配置gradle代理
dependencies {
//    implementation "com.itextpdf:itext-core:${itextVersion}"

    // 使用该依赖打包会出现 错误: 当前不支持已签名模块化 JAR ..\QuickOutline\build\jlinkbase\jlinkjars\bcpkix-jdk18on-1.78.jar,
//    https://stackoverflow.com/questions/50597926/signed-modular-jar-with-crypto-provider-cannot-be-linked-into-run-time-image
    implementation "com.itextpdf:bouncy-castle-adapter:${itextVersion}" //签名用，加密的PDF必须添加
    implementation "com.itextpdf:kernel:${itextVersion}"
    implementation "com.itextpdf:io:${itextVersion}"
    implementation "com.itextpdf:layout:${itextVersion}"
    implementation "com.itextpdf:font-asian:${itextVersion}"

    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'

    implementation 'org.slf4j:slf4j-api:2.0.16' // Use the latest stable version
    implementation 'org.slf4j:slf4j-simple:2.0.16' // Or another implementation

    // Apache PDFBox 核心库
    implementation 'org.apache.pdfbox:pdfbox:3.0.3'
    // 如果需要处理加密的 PDF 文件
    implementation 'org.apache.pdfbox:pdfbox-tools:3.0.3'
    // 如果需要字体支持（比如渲染中文等非西文字符）
    implementation 'org.apache.pdfbox:fontbox:3.0.3'
}

repositories {
    //使用https，http会报警告
//    maven { url 'https://maven.aliyun.com/repository/public/' }
//    maven { url "https://maven.aliyun.com/repository/gradle-plugin" }
//    maven { url 'https://mirrors.cloud.tencent.com/gradle/'}
    gradlePluginPortal()
    mavenLocal()
    mavenCentral()
}

//传递版本号给Java程序，将在System.System.getProperty("app.version")获取
def args = ['-Dapp.version=' + project.version]

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
//    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" // 调试用
}


//tasks.register('generateVersionProperties') {
//    doFirst {
//        def propertiesFile = new File(project.layout.buildDirectory.asFile.get(), "resources${File.separator}version.properties")
//        propertiesFile.text = "version=${version}"
//    }
//}
//
//processResources.dependsOn(generateVersionProperties)  // 确保在资源处理之前生成version.properties
//

// 仅对application中的run任务有效，对build中任务无效
run {
    jvmArgs = args
}


def os = org.gradle.internal.os.OperatingSystem.current()

//https://badass-jlink-plugin.beryx.org/releases/latest/
// 仅对build中任务有效，对application中的run任务无效，
jlink {
    launcher {
        name = 'QuickOutline'
        jvmArgs = args
    }
//    压缩问题：https://github.com/fvarrui/JavaPackager/issues/383
//     “警告: 当前不支持已签名模块化" 加入'--ignore-signing-information'选项
    options = ['--strip-debug', '--compress', 'zip-6', '--no-header-files', '--no-man-pages', '--ignore-signing-information']
//    https://walczak.it/blog/distributing-javafx-desktop-applications-without-requiring-jvm-using-jlink-and-jpackage
    jpackage {
//        skipInstaller = true
        def installerOptionsParams = [
                '--vendor', 'riri Personal', //必须加，否则报错
                '--app-version', (String) version,
        ]

        println('Current OS: '+os)

        if (os.isWindows()){
            icon = 'src/main/resources/com/ririv/quickoutline/view/icon/icon.ico'
            installerType = 'msi' //或exe
            installerOptions = [
                    '--win-shortcut',
                    '--win-dir-chooser',
                    '--win-menu'
            ]+installerOptionsParams
        }
        else if (os.isMacOsX()) {
            def entitlementsFile = file("src/main/resources/entitlements.plist")  // 权限文件路径
            icon = 'src/main/resources/com/ririv/quickoutline/view/icon/icon.icns'
            installerType = 'pkg' //dmg或pkg
            installerOptions = installerOptionsParams
//            + ['--mac-sign',                         // 启用签名
////            '--mac-signing-keychain', '/Library/Keychains/System.keychain', // 钥匙串路径
////            '--mac-signing-key-user-name', "Developer ID Application: Your Name (XXXXXXXXXX)", // 证书名称
//            '--mac-entitlements', entitlementsFile.absolutePath ] // 权限文件
        }
    }

}







